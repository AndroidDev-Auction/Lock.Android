def loadAuth0Properties(filename) {
    Properties props = new Properties()
    props.load(new FileInputStream(file(rootProject.file(filename))))
    return props
}

apply plugin: 'com.android.application'

android {

    preBuild.doFirst {
        assert file(rootProject.file("auth0.properties.debug")).exists()
        assert file(rootProject.file("auth0.properties.release")).exists()
    }

    compileSdkVersion 21
    buildToolsVersion "21.1.1"

    defaultConfig {
        applicationId "com.auth0.app.Lock"
        minSdkVersion 14
        targetSdkVersion 21
        versionCode 1
        versionName "1.0.0"
    }

    buildTypes {

        debug {
            def auth0 = loadAuth0Properties("auth0.properties.debug")
            resValue "string", "auth0_client_id", auth0["clientId"]
            resValue "string", "auth0_tenant_name", auth0["tenantName"]
        }

        release {
            def auth0 = loadAuth0Properties("auth0.properties.release")
            resValue "string", "auth0_client_id", auth0["clientId"]
            resValue "string", "auth0_tenant_name", auth0["tenantName"]
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':lock')
    compile project(':facebook')
    compile project(':googleplus')
    compile 'com.android.support:appcompat-v7:21.+'
}
